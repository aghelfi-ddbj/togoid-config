PREFIX dct: <http://purl.org/dc/terms/>
PREFIX tgvo: <http://togovar.biosciencedbc.jp/vocabulary/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cvo: <http://purl.jp/bio/10/clinvar/>

SELECT DISTINCT ?tgv_id ?medgen_id
WHERE  { 
  GRAPH <http://rdf.integbio.jp/dataset/togosite/variation> {
    ?variation dct:identifier ?tgv_id .
  }
  GRAPH <http://rdf.integbio.jp/dataset/togosite/variation/annotation/clinvar> {
    ?variation tgvo:condition/rdfs:seeAlso ?clinvar .
  }
  GRAPH <http://rdf.integbio.jp/dataset/togosite/clinvar> {
    ?clinvar cvo:interpreted_record/cvo:rcv_list/cvo:rcv_accession ?_rcv .
    ?_rcv cvo:interpreted_condition_list/cvo:interpreted_condition/dct:references ?medgen .
  }
  BIND (strafter(str(?medgen), "http://ncbi.nlm.nih.gov/medgen/") AS ?medgen_id)
}
