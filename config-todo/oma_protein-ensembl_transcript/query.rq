# Endpoint: https://sparql.omabrowser.org/sparql/
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX lscr: <http://purl.org/lscr#>
PREFIX orth: <http://purl.org/net/orth#>

SELECT DISTINCT ?omaprotein_id ?enst_id
WHERE {
  ?omaprotein orth:organism/obo:RO_0002162 <http://purl.uniprot.org/taxonomy/9606> .
  # RO_0002162 'in taxon'
  ?omaprotein lscr:xrefEnsemblTranscript ?enst .

  BIND (replace(str(?omaprotein), 'https://omabrowser.org/oma/info/', '') AS ?omaprotein_id)
  BIND (replace(str(?enst), 'http://rdf.ebi.ac.uk/resource/ensembl.transcript/', '') AS ?enst_id)
}
